/*
"Proyecto Scavenger" - TranZit / Die Rise / Buried
v1.7

Creado por: NickB_05

 Este script te permite llevar todas las piezas construibles de los mapas
 del grupo Victis, de forma similar al sistema de transporte de piezas
 de Mob, Origins y BO3.

 Mi objetivo es que el sistema de transporte de piezas sea lo más parecido
 posible al de Mob y Origins, incluyendo elementos de interfaz similares
 y mostrando las piezas en la tabla de puntuaciones; por ahora no es así,
 ya que sigo aprendiendo a implementar la interfaz, pero al menos el
 concepto es 100% fiel al original.

 Las únicas piezas que no se pueden llevar todas a la vez son la llave del ascensor,
 la llave de la prisión, el licor, los caramelos y las tizas de armas; esto es
 principalmente para mantener las mecánicas y... porque tengo algunas ideas
 para la llave del ascensor... ¡disfruta del script!

 AVISO: Si vas a utilizar este script para otro proyecto, por favor
 da crédito a mi trabajo, ya que tardé al menos un mes en terminarlo.

Correcciones de la v1.1 realizadas por: SyntaXError
Correcciones de la v1.2 realizadas por: NickB_05
Correcciones v1.3 y v1.4 para multijugador realizadas por: NickB_05
Actualizacion v1.5, y v1.6 del Leaderboard realizadas por: NickB_05
Actualizacion v1.7 de la Llave del Elevador realizada por: NickB_05
*/

#include maps\mp\zombies\_zm_buildables;
#include maps\mp\zombies\_zm_weapons;
#include maps\mp\zombies\_zm_utility;
#include maps\mp\gametypes_zm\_hud_util;
#include maps\mp\_utility;

#define MC_BUILD_RADIUS_SQ 7000 // distancia horizontal (X/Y) para la mayoría de las áreas edificables
#define MC_BUILD_RADIUS_SQ_TIGHT 2500 // Escotilla/escalera/arado: colocados más juntos para evitar pisar la zona de reparación de la ventana u otros elementos cercanos
#define MC_HEIGHT_TOLERANCE 82 // Diferencia de altura máxima permitida (Z): filtra los distintos niveles (generalmente separados por 128 unidades o más) sin interferir con la construcción estándar
#define MC_DEFAULT_BUILD_TIME 3000 // ms, se utiliza si el stub no trae su propio tiempo de uso

#define MC_TAB_SQUARE_X 91  // posicion horizontal (desde la esquina sup. izq., escala 640)
#define MC_TAB_SQUARE_Y 97 // posicion vertical base: sin piezas, o buildable ya construido
#define MC_TAB_SQUARE_Y_ACTIVE 87 // posicion vertical cuando ese slot tiene >=1 pieza y no esta construido
#define MC_TAB_SQUARE_SIZE 29 // ancho/alto de cada cuadrado negro (baja este numero para achicarlo)
#define MC_TAB_BORDER_PAD 2 // grosor del borde gris a cada lado del cuadrado
#define MC_TAB_SLOT_GAP 6 // espacio horizontal entre cuadrados de la fila
#define MC_TAB_CHECK_SIZE 10 // tamano del checkmark (zm_hud_icon_sq_scafold) en la esquina inf. der.
#define MC_TAB_LOCK_SIZE 10 // tamano de la cruz roja (zm_hud_icon_fan) cuando el buildable esta bloqueado por su par (horca/guillotina)

#define MC_NAVCARD_X 634 // horizontal position of the navcard square (tip opposite the row)
#define MC_NAVCARD_Y 97 // vertical position of the navcard square

#define MC_KEY_COOLDOWN_MS 15000 // ms de espera por jugador entre usos de la Llave del Elevador
#define MC_KEY_INSERT_TIME 500 // ms que tarda en insertarse la Llave del Elevador (mantener [usar])

init()
{
    map = getdvar( "mapname" );

    if ( map != "zm_transit" && map != "zm_highrise" && map != "zm_buried" )
        return;

    precacheshader( "zm_hud_icon_sq_scafold" );
    precacheshader( "zm_hud_icon_sq_tranceiver" );
    precacheshader( "zm_hud_icon_fan" );
    precacheshader( "zom_hud_icon_epod_key" );

    if ( map == "zm_buried" )
    {
        func = getfunction( "maps/mp/zombies/_zm_buildables_pooled", "pooledbuildable_stub_for_piece" );
        if ( isdefined( func ) )
        {
            replacefunc( func, ::custom_pooledbuildable_stub_for_piece );
        }
    }

    level.mc_is_buried = ( map == "zm_buried" );
    level.mc_elevator_is_on_floor_func = undefined;
    level.mc_elevator_level_for_floor_func = undefined;

    if ( map == "zm_highrise" )
    {
        level.mc_elevator_is_on_floor_func = getfunction( "maps/mp/zm_highrise_elevators", "elevator_is_on_floor" );
        level.mc_elevator_level_for_floor_func = getfunction( "maps/mp/zm_highrise_elevators", "elevator_level_for_floor" );
    }

    level.mc_have = [];

    level.mc_debug = 0;
    if ( getdvar( "mc_debug" ) == "1" )
        level.mc_debug = 1;

    level.mc_gated_buildables = [];
    level.mc_gated_buildables["jetgun_zm"] = 1;
    level.mc_gated_buildables["turbine"] = 1;
    level.mc_gated_buildables["riotshield_zm"] = 1;
    level.mc_gated_buildables["turret"] = 1;
    level.mc_gated_buildables["electric_trap"] = 1;
    level.mc_gated_buildables["powerswitch"] = 1;
    level.mc_gated_buildables["pap"] = 1;
    level.mc_gated_buildables["sq_common"] = 1;
    level.mc_gated_buildables["springpad_zm"] = 1; 
    level.mc_gated_buildables["slipgun_zm"] = 1;
    level.mc_gated_buildables["headchopper_zm"] = 1;
    level.mc_gated_buildables["subwoofer_zm"] = 1;
    level.mc_gated_buildables["buried_sq_bt_m_tower"] = 1;
    level.mc_gated_buildables["buried_sq_bt_r_tower"] = 1;
    level.mc_immediate_buildables = [];
    level.mc_immediate_buildables["cattlecatcher"] = 1;
    level.mc_immediate_buildables["bushatch"] = 1;
    level.mc_immediate_buildables["dinerhatch"] = 1;
    level.mc_immediate_buildables["busladder"] = 1;

    level.mc_key_buildables = [];

    if ( map == "zm_highrise" )
    {
        level.mc_key_buildables["ekeys_zm"] = 1;
        level.mc_immediate_buildables["ekeys_zm"] = 1;
    }

    level thread on_player_connect();
    level thread mc_debug_print_names();
    level thread mc_setup_custom_prompts();
}

mc_debug_print_names()
{
    level waittill( "buildables_setup" );

    if ( !level.mc_debug )
        return;

    foreach ( stub in level.buildable_stubs )
    {
        if ( !isdefined( stub.buildablezone ) )
            continue;

        name = stub.buildablezone.buildable_name;
        gated = isdefined( level.mc_gated_buildables[name] );
        immediate = isdefined( level.mc_immediate_buildables[name] );
        println( "[mc_debug] buildable_name = " + name + "  (gated=" + gated + ", immediate=" + immediate + ")" );
    }
}

mc_display_name( name )
{
    switch ( name )
    {
        case "riotshield_zm":
            return "Escudo Zombi";
        case "jetgun_zm":
            return "Jet Gun";
        case "turbine":
            return "Turbina";
        case "turret":
            return "Torreta";
        case "electric_trap":
            return "Trampa Electrica";
        case "powerswitch":
            return "Interruptor de Energia";
        case "pap":
            return "Potenciadora";
        case "sq_common":
            return "Tarjeta-Nav";
        case "springpad_zm":
            return "Trampolin de Aire";
        case "slipgun_zm":
            return "Esliquificador";
        case "headchopper_zm":
            return "Corta Cabezas";
        case "subwoofer_zm":
        case "subwoofer":
            return "Resonador";
        case "buried_sq_bt_m_tower":
            return "Horcas";
        case "buried_sq_bt_r_tower":
            return "Guillotinas";
        case "cattlecatcher":
            return "Parachoques";
        case "bushatch":
            return "Escotilla del Bus";
        case "dinerhatch":
            return "Escotilla de Cafeteria";
        case "busladder":
            return "Escalera del Bus";
        case "ekeys_zm":
            return "Llave del Elevador";
    }

    return name;
}

mc_representative_icon( name )
{
    switch ( name )
    {
        case "riotshield_zm":
            return "riotshield_zm_icon";
        case "jetgun_zm":
            return "jetgun_zm_icon";
        case "turbine":
            return "turbine_zm_icon";
        case "turret":
            return "turret_zm_icon";
        case "electric_trap":
            return "etrap_zm_icon";
        case "powerswitch":
            return "zm_hud_icon_panel";
        case "pap":
            return "zm_hud_icon_papbody";
        case "sq_common":
            return "zm_hud_icon_sq_powerbox";
        case "springpad_zm":
            return "zom_hud_trample_steam_complete";
        case "slipgun_zm":
            return "zom_hud_icon_buildable_slip_ext";
        case "headchopper_zm":
            return "zom_hud_icon_buildable_chop_a";
        case "subwoofer_zm":
        case "subwoofer":
            return "zom_hud_icon_buildable_woof_speaker";
        case "buried_sq_bt_m_tower":
            return "zm_hud_icon_battery";
        case "buried_sq_bt_r_tower":
            return "zm_hud_icon_sq_meteor";
        case "cattlecatcher":
            return "zm_hud_icon_plow";
        case "bushatch":
            return "zm_hud_icon_hatch";
        case "dinerhatch":
            return "zm_hud_icon_hatch";
        case "busladder":
            return "zm_hud_icon_ladder";
        case "ekeys_zm":
            return "zom_hud_icon_epod_key";
    }

    return undefined;
}

mc_show_piece_notify( display_name, hud_icon, progress_text )
{
    self endon( "disconnect" );

    if ( isdefined( self.mc_notify_icon ) )
        self.mc_notify_icon destroy();

    if ( isdefined( self.mc_notify_text ) )
        self.mc_notify_text destroy();

    icon = newclienthudelem( self );
    icon.horzalign = "left";
    icon.vertalign = "top";
    icon.alignx = "right";
    icon.aligny = "top";
    icon.x = -12;
    icon.y = -23;
    icon.alpha = 1;

    if ( isdefined( hud_icon ) )
        icon setshader( hud_icon, 20, 20 );

    self.mc_notify_icon = icon;
    icon thread mc_fade_and_destroy( 2.5 );

    if ( !isdefined( display_name ) )
        return;

    text = newclienthudelem( self );
    text.horzalign = "left";
    text.vertalign = "top";
    text.alignx = "left";
    text.aligny = "top";
    text.x = -8;
    text.y = -23;
    text.fontscale = 1.3;
    text.alpha = 1;

    if ( isdefined( progress_text ) )
        text settext( display_name + " (" + progress_text + ")" );
    else
        text settext( display_name );

    self.mc_notify_text = text;

    text thread mc_fade_and_destroy( 2.5 );
}

mc_show_key_pickup_notify( hud_icon )
{
    self endon( "disconnect" );

    if ( isdefined( self.mc_key_notify_icon ) )
        self.mc_key_notify_icon destroy();

    if ( isdefined( self.mc_key_notify_text ) )
        self.mc_key_notify_text destroy();

    icon = newclienthudelem( self );
    icon.horzalign = "left";
    icon.vertalign = "top";
    icon.alignx = "right";
    icon.aligny = "top";
    icon.x = -12;
    icon.y = -23;
    icon.alpha = 1;

    if ( isdefined( hud_icon ) )
        icon setshader( hud_icon, 20, 20 );

    text = newclienthudelem( self );
    text.horzalign = "left";
    text.vertalign = "top";
    text.alignx = "left";
    text.aligny = "top";
    text.x = -8;
    text.y = -23;
    text.fontscale = 1.3;
    text.alpha = 1;
    text settext( "Llave del Elevador" );

    self.mc_key_notify_icon = icon;
    self.mc_key_notify_text = text;

    icon thread mc_fade_and_destroy( 2.5 );
    text thread mc_fade_and_destroy( 2.5 );
}

mc_fade_and_destroy( delay )
{
    self endon( "death" );
    wait delay;

    if ( !isdefined( self ) )
        return;

    self fadeovertime( 0.5 );
    self.alpha = 0;
    wait 0.5;

    if ( isdefined( self ) )
        self destroy();
}

mc_is_ours( name )
{
    return isdefined( level.mc_gated_buildables[name] ) || isdefined( level.mc_immediate_buildables[name] ) || isdefined( level.mc_key_buildables[name] );
}

mc_is_gated( name )
{
    return isdefined( level.mc_gated_buildables[name] );
}

mc_is_key( name )
{
    return isdefined( level.mc_key_buildables[name] );
}

mc_is_buried_fixed( name )
{
    return name == "buried_sq_bt_m_tower" || name == "buried_sq_bt_r_tower";
}

mc_other_buried_tower( name )
{
    if ( name == "buried_sq_bt_m_tower" )
        return "buried_sq_bt_r_tower";

    if ( name == "buried_sq_bt_r_tower" )
        return "buried_sq_bt_m_tower";

    return undefined;
}

mc_buried_tower_locked( name )
{
    if ( !level.mc_is_buried || !mc_is_buried_fixed( name ) )
        return false;

    other_name = mc_other_buried_tower( name );

    if ( !isdefined( other_name ) )
        return false;

    other_stub = mc_find_stub_by_buildable_name( other_name );

    return isdefined( other_stub ) && isdefined( other_stub.built ) && other_stub.built;
}

mc_in_range( origin, target, radius_sq )
{
    if ( distance2dsquared( origin, target ) >= radius_sq )
        return false;

    zdiff = origin[2] - target[2];

    if ( zdiff < 0 )
        zdiff = zdiff * -1;

    if ( zdiff > MC_HEIGHT_TOLERANCE )
        return false;

    return true;
}

mc_build_radius_sq( name )
{
    switch ( name )
    {
        case "bushatch":
        case "dinerhatch":
        case "busladder":
        case "cattlecatcher":
            return MC_BUILD_RADIUS_SQ_TIGHT;
    }

    return MC_BUILD_RADIUS_SQ;
}

mc_setup_custom_prompts()
{
    level waittill( "buildables_setup" );
	
    level.mc_buildables_ready = true;
    level.mc_stub_by_name = [];

    ours_stubs = [];
    key_samples = [];

    foreach ( stub in level.buildable_stubs )
    {
        if ( !isdefined( stub.buildablezone ) )
            continue;

        if ( !mc_is_ours( stub.buildablezone.buildable_name ) )
            continue;

        level.mc_stub_by_name[stub.buildablezone.buildable_name] = stub;

        stub.mc_original_prompt = stub.custom_buildablestub_update_prompt;
        stub.custom_buildablestub_update_prompt = ::mc_custom_prompt;

        ours_stubs[ours_stubs.size] = stub;

        zone = stub.buildablezone;

        if ( isdefined( zone.pieces ) )
        {
            for ( i = 0; i < zone.pieces.size; i++ )
            {
                pkey = mc_piece_key( zone.pieces[i] );

                if ( !isdefined( key_samples[pkey] ) )
                    key_samples[pkey] = zone.pieces[i];
            }
        }
    }

    level.mc_key_stubs = [];
    level.mc_key_piece_key = undefined;

    foreach ( stub in ours_stubs )
    {
        if ( !mc_is_key( stub.buildablezone.buildable_name ) )
            continue;

        level.mc_key_stubs[level.mc_key_stubs.size] = stub;

        if ( !isdefined( level.mc_key_piece_key ) && isdefined( stub.buildablezone.pieces ) && stub.buildablezone.pieces.size > 0 )
            level.mc_key_piece_key = mc_piece_key( stub.buildablezone.pieces[0] );
    }

    level.mc_piece_candidates = [];

    if ( !level.mc_is_buried )
    {
        foreach ( sample in key_samples )
        {
            pkey = mc_piece_key( sample );
            candidates = [];

            foreach ( cand_stub in ours_stubs )
            {
                if ( isdefined( cand_stub.buildablezone ) && cand_stub.buildablezone buildable_has_piece( sample ) )
                    candidates[candidates.size] = cand_stub;
            }

            level.mc_piece_candidates[pkey] = candidates;
        }
    }
}

mc_buried_find_ready_target()
{
    foreach ( stub in level.buildable_stubs )
    {
        if ( !isdefined( stub.buildablezone ) || !mc_is_ours( stub.buildablezone.buildable_name ) )
            continue;

        if ( mc_is_buried_fixed( stub.buildablezone.buildable_name ) )
            continue;

        if ( isdefined( stub.table_built ) && stub.table_built )
            continue;

        if ( isdefined( stub.built ) && stub.built )
            continue;

        zone = stub.buildablezone;
        deliverable = self mc_get_deliverable_pieces( zone );
        can_attempt = false;

        if ( mc_is_gated( zone.buildable_name ) )
            can_attempt = deliverable.size > 0 && deliverable.size == mc_count_remaining( zone );
        else
            can_attempt = deliverable.size > 0;

        if ( can_attempt )
            return stub;
    }

    return undefined;
}

mc_custom_prompt( player )
{
    if ( isdefined( self.built ) && self.built )
        return true;

    if ( isdefined( self.buildablezone ) && mc_is_key( self.buildablezone.buildable_name ) )
        return self mc_key_prompt_logic( player );

    if ( isdefined( self.mc_original_prompt ) && !( self [[ self.mc_original_prompt ]]( player ) ) )
        return false;

    if ( !isdefined( self.buildablezone ) )
        return true;

    zone = self.buildablezone;

    if ( !mc_is_ours( zone.buildable_name ) )
        return true;

    if ( mc_buried_tower_locked( zone.buildable_name ) )
        return true;

    deliverable = player mc_get_deliverable_pieces( zone );
    ready = false;

    if ( mc_is_gated( zone.buildable_name ) )
        ready = deliverable.size > 0 && deliverable.size == mc_count_remaining( zone );
    else
        ready = deliverable.size > 0;

    display_name = zone.buildable_name;

    if ( !ready && level.mc_is_buried && !mc_is_buried_fixed( zone.buildable_name ) )
    {
        target = player mc_buried_find_ready_target();

        if ( isdefined( target ) )
        {
            ready = true;
            display_name = target.buildablezone.buildable_name;
        }
    }

    if ( ready )
    {
        if ( isdefined( level.zombie_buildables[self.equipname] ) && isdefined( level.zombie_buildables[self.equipname].hint ) )
            self.hint_string = level.zombie_buildables[self.equipname].hint;
			
        self.cursor_hint = "HINT_NOICON";
        return false;
    }

    return true;
}

mc_key_prompt_logic( player )
{
    if ( !isdefined( player.mc_has_key ) || !player.mc_has_key )
        return true;

    if ( !mc_key_resolve_elevator( self ) )
        return true;

    if ( isdefined( level.mc_elevator_is_on_floor_func ) && self.elevator [[ level.mc_elevator_is_on_floor_func ]]( self.floor ) )
        return true;

    remaining = player mc_key_cooldown_remaining();

    if ( remaining > 0 )
    {
        self.hint_string = "Llave recuperandose...";
        self.cursor_hint = "HINT_NOICON";
        return false;
    }
	
    if ( isdefined( level.zombie_buildables[self.equipname] ) && isdefined( level.zombie_buildables[self.equipname].hint ) )
        self.hint_string = level.zombie_buildables[self.equipname].hint;

    self.cursor_hint = "HINT_NOICON";
    return false;
}

mc_key_resolve_elevator( stub )
{
    if ( isdefined( stub.elevator ) && isdefined( stub.floor ) )
        return true;

    elevatorname = stub.script_noteworthy;

    if ( !isdefined( elevatorname ) || !isdefined( stub.script_parameters ) )
        return false;

    if ( !isdefined( level.elevators ) || !isdefined( level.elevators[elevatorname] ) )
        return false;

    elevator = level.elevators[elevatorname];
    floor = int( stub.script_parameters );

    stub.elevator = elevator;

    if ( isdefined( level.mc_elevator_level_for_floor_func ) )
        stub.floor = elevator [[ level.mc_elevator_level_for_floor_func ]]( floor );

    return true;
}

mc_key_cooldown_remaining()
{
    if ( !isdefined( self.mc_key_cooldown_end ) )
        return 0;

    remaining_ms = self.mc_key_cooldown_end - gettime();

    if ( remaining_ms <= 0 )
        return 0;

    return int( remaining_ms / 1000 ) + 1;
}

on_player_connect()
{
    level endon( "end_game" );

    while ( true )
    {
        level waittill( "connected", player );
        player thread player_collect_and_build();
        player thread mc_tab_square_watch();
    }
}

mc_tab_buildable_list()
{
    map = getdvar( "mapname" );

    list = [];
	
	if ( map == "zm_highrise" )
    {
        list[0] = "slipgun_zm";
        list[1] = "springpad_zm";
        return list;
    }

    if ( map == "zm_buried" )
    {
        list[0] = "turbine";
        list[1] = "springpad_zm";
        list[2] = "subwoofer_zm";
        list[3] = "headchopper_zm";
        return list;
    }
	
    list[0] = "turbine";
    list[1] = "riotshield_zm";
    list[2] = "turret";
    list[3] = "electric_trap";
    list[4] = "jetgun_zm";
    return list;
}

mc_tab_attached_list()
{
    map = getdvar( "mapname" );

    list = [];

    if ( map == "zm_highrise" )
    {
        list[0] = "ekeys_zm"; 
        return list;
    }

    if ( map == "zm_buried" )
    {
        list[0] = "buried_sq_bt_m_tower"; 
        list[1] = "buried_sq_bt_r_tower";
        return list;
    }

    list[0] = "cattlecatcher";
    list[1] = "bushatch";
    list[2] = "busladder";
    return list;
}

mc_tab_square_watch()
{
    self endon( "disconnect" );

    self notifyonplayercommand( "mc_tab_down", "+scores" );
    self notifyonplayercommand( "mc_tab_up", "-scores" );

    self.mc_tab_held = false;

    self thread mc_tab_down_listener();
    self thread mc_tab_up_listener();

    list = mc_tab_buildable_list();
    attached_list = mc_tab_attached_list();

    borders = [];
    icons = [];
    attached_borders = [];
    attached_icons = [];
    navcard_border = undefined;
    navcard_icon = undefined;
    shown = false;

    while ( true )
    {
        if ( self.mc_tab_held && !shown )
        {
            self.mc_tab_counters = [];
            self.mc_tab_checks = [];

            for ( i = 0; i < list.size; i++ )
            {
                slot_x = MC_TAB_SQUARE_X + i * ( MC_TAB_SQUARE_SIZE + MC_TAB_BORDER_PAD * 2 + MC_TAB_SLOT_GAP );

                border = newclienthudelem( self );
                border.horzalign = "left";
                border.vertalign = "top";
                border.alignx = "center";
                border.aligny = "middle";
                border.x = slot_x;
                border.y = MC_TAB_SQUARE_Y;
                border.alpha = 0.7;
                border.color = ( 1, 1, 1 );
                border.sort = 1;
                border setshader( "zm_hud_icon_sq_tranceiver", MC_TAB_SQUARE_SIZE + MC_TAB_BORDER_PAD * 2, MC_TAB_SQUARE_SIZE + MC_TAB_BORDER_PAD * 2 );

                icon = newclienthudelem( self );
                icon.horzalign = "left";
                icon.vertalign = "top";
                icon.alignx = "center";
                icon.aligny = "middle";
                icon.x = slot_x;
                icon.y = MC_TAB_SQUARE_Y;
                icon.alpha = 0.5;
                icon.sort = 3;

                icon_shader = mc_representative_icon( list[i] );
                if ( isdefined( icon_shader ) )
                    icon setshader( icon_shader, MC_TAB_SQUARE_SIZE - MC_TAB_BORDER_PAD * 2, MC_TAB_SQUARE_SIZE - MC_TAB_BORDER_PAD * 2 );

                borders[i] = border;
                icons[i] = icon;
            }

            slot_pitch = MC_TAB_SQUARE_SIZE + MC_TAB_BORDER_PAD * 2 + MC_TAB_SLOT_GAP;

            for ( i = 0; i < attached_list.size; i++ )
            {
                slot_x = MC_NAVCARD_X - ( attached_list.size - i ) * slot_pitch;

                border = newclienthudelem( self );
                border.horzalign = "left";
                border.vertalign = "top";
                border.alignx = "center";
                border.aligny = "middle";
                border.x = slot_x;
                border.y = MC_TAB_SQUARE_Y;
                border.alpha = 0.7;
                border.color = ( 1, 1, 1 );
                border.sort = 1;
                border setshader( "zm_hud_icon_sq_tranceiver", MC_TAB_SQUARE_SIZE + MC_TAB_BORDER_PAD * 2, MC_TAB_SQUARE_SIZE + MC_TAB_BORDER_PAD * 2 );

                icon = newclienthudelem( self );
                icon.horzalign = "left";
                icon.vertalign = "top";
                icon.alignx = "center";
                icon.aligny = "middle";
                icon.x = slot_x;
                icon.y = MC_TAB_SQUARE_Y;
                icon.alpha = 0.5;
                icon.sort = 3;

                icon_shader = mc_representative_icon( attached_list[i] );
                if ( isdefined( icon_shader ) )
                    icon setshader( icon_shader, MC_TAB_SQUARE_SIZE - MC_TAB_BORDER_PAD * 2, MC_TAB_SQUARE_SIZE - MC_TAB_BORDER_PAD * 2 );

                attached_borders[i] = border;
                attached_icons[i] = icon;
            }

            navcard_border = newclienthudelem( self );
            navcard_border.horzalign = "left";
            navcard_border.vertalign = "top";
            navcard_border.alignx = "center";
            navcard_border.aligny = "middle";
            navcard_border.x = MC_NAVCARD_X;
            navcard_border.y = MC_NAVCARD_Y;
            navcard_border.alpha = 0.7;
            navcard_border.color = ( 1, 1, 1 );
            navcard_border.sort = 1;
            navcard_border setshader( "zm_hud_icon_sq_tranceiver", MC_TAB_SQUARE_SIZE + MC_TAB_BORDER_PAD * 2, MC_TAB_SQUARE_SIZE + MC_TAB_BORDER_PAD * 2 );

            navcard_icon = newclienthudelem( self );
            navcard_icon.horzalign = "left";
            navcard_icon.vertalign = "top";
            navcard_icon.alignx = "center";
            navcard_icon.aligny = "middle";
            navcard_icon.x = MC_NAVCARD_X;
            navcard_icon.y = MC_NAVCARD_Y;
            navcard_icon.alpha = 0.5;
            navcard_icon.sort = 3;

            icon_shader = mc_representative_icon( "sq_common" );
            if ( isdefined( icon_shader ) )
                navcard_icon setshader( icon_shader, MC_TAB_SQUARE_SIZE - MC_TAB_BORDER_PAD * 2, MC_TAB_SQUARE_SIZE - MC_TAB_BORDER_PAD * 2 );

            shown = true;
        }
        else if ( !self.mc_tab_held && shown )
        {
            for ( i = 0; i < list.size; i++ )
            {
                if ( isdefined( icons[i] ) )
                    icons[i] destroy();

                if ( isdefined( borders[i] ) )
                    borders[i] destroy();
            }

            borders = [];
            icons = [];

            for ( i = 0; i < attached_list.size; i++ )
            {
                if ( isdefined( attached_icons[i] ) )
                    attached_icons[i] destroy();

                if ( isdefined( attached_borders[i] ) )
                    attached_borders[i] destroy();
            }

            attached_borders = [];
            attached_icons = [];

            if ( isdefined( navcard_icon ) )
                navcard_icon destroy();

            if ( isdefined( navcard_border ) )
                navcard_border destroy();

            navcard_border = undefined;
            navcard_icon = undefined;

            if ( isdefined( self.mc_tab_counters ) )
            {
                foreach ( counter_elem in self.mc_tab_counters )
                {
                    if ( isdefined( counter_elem ) )
                        counter_elem destroy();
                }
            }

            if ( isdefined( self.mc_tab_checks ) )
            {
                foreach ( check_elem in self.mc_tab_checks )
                {
                    if ( isdefined( check_elem ) )
                        check_elem destroy();
                }
            }

            if ( isdefined( self.mc_tab_locks ) )
            {
                foreach ( lock_elem in self.mc_tab_locks )
                {
                    if ( isdefined( lock_elem ) )
                        lock_elem destroy();
                }
            }

            self.mc_tab_counters = [];
            self.mc_tab_checks = [];
            self.mc_tab_locks = [];

            shown = false;
        }

        if ( shown )
        {
            for ( i = 0; i < list.size; i++ )
            {
                slot_x = MC_TAB_SQUARE_X + i * ( MC_TAB_SQUARE_SIZE + MC_TAB_BORDER_PAD * 2 + MC_TAB_SLOT_GAP );
                self mc_update_tab_slot_hud( list[i], slot_x, borders[i], icons[i] );
            }

            for ( i = 0; i < attached_list.size; i++ )
            {
                slot_x = MC_NAVCARD_X - ( attached_list.size - i ) * ( MC_TAB_SQUARE_SIZE + MC_TAB_BORDER_PAD * 2 + MC_TAB_SLOT_GAP );
                self mc_update_tab_slot_hud( attached_list[i], slot_x, attached_borders[i], attached_icons[i] );
            }

            self mc_update_tab_slot_hud( "sq_common", MC_NAVCARD_X, navcard_border, navcard_icon );
        }

        wait 0.05;
    }
}

mc_tab_down_listener()
{
    self endon( "disconnect" );

    while ( true )
    {
        self waittill( "mc_tab_down" );
        self.mc_tab_held = true;
    }
}

mc_tab_up_listener()
{
    self endon( "disconnect" );

    while ( true )
    {
        self waittill( "mc_tab_up" );
        self.mc_tab_held = false;
    }
}

mc_find_stub_by_buildable_name( name )
{
    if ( !level.mc_is_buried && isdefined( level.mc_stub_by_name ) && isdefined( level.mc_stub_by_name[name] ) )
        return level.mc_stub_by_name[name];

    foreach ( stub in level.buildable_stubs )
    {
        if ( isdefined( stub.buildablezone ) && stub.buildablezone.buildable_name == name )
            return stub;
    }

    return undefined;
}

mc_update_tab_slot_hud( name, slot_x, border, icon )
{
    square_y = MC_TAB_SQUARE_Y;

    stub = mc_find_stub_by_buildable_name( name );

    need_counter = false;
    need_check = false;
    need_lock = false;
    have = 0;
    total = 0;

    if ( !isdefined( stub ) )
    {
        icon.alpha = 0;
    }
    else
    {
        zone = stub.buildablezone;
        is_built = isdefined( stub.built ) && stub.built;

        if ( name == "bushatch" || name == "dinerhatch" )
        {
            other_name = ( name == "bushatch" ) ? "dinerhatch" : "bushatch";
            other_stub = mc_find_stub_by_buildable_name( other_name );

            if ( isdefined( other_stub ) && isdefined( other_stub.built ) && other_stub.built )
                is_built = true;
        }

        built_count = 0;

        for ( i = 0; i < zone.pieces.size; i++ )
        {
            if ( isdefined( zone.pieces[i].built ) && zone.pieces[i].built )
                built_count++;
        }

        deliverable = self mc_get_deliverable_pieces( zone );
        have = built_count + deliverable.size;
        total = zone.pieces.size;

        is_immediate_style = isdefined( level.mc_immediate_buildables[name] );

        if ( is_immediate_style )
        {
            if ( is_built )
            {
                icon.alpha = 1;
                need_check = true;
            }
            else if ( have > 0 )
            {
                icon.alpha = 1;
            }
            else
            {
                icon.alpha = 0.5;
            }
        }
        else if ( is_built )
        {
            icon.alpha = 1;
            need_check = true;
        }
        else if ( have > 0 )
        {
            icon.alpha = 0.5;
            need_counter = true;
            square_y = MC_TAB_SQUARE_Y_ACTIVE;
        }
        else
        {
            icon.alpha = 0.5;
        }
        if ( mc_buried_tower_locked( name ) )
        {
            need_lock = true;
            need_counter = false;
            need_check = false;
            icon.alpha = 0.5;
            square_y = MC_TAB_SQUARE_Y;
        }
    }

    if ( isdefined( border ) )
        border.y = square_y;

    icon.y = square_y;

    if ( !isdefined( self.mc_tab_counters ) )
        self.mc_tab_counters = [];

    if ( !isdefined( self.mc_tab_checks ) )
        self.mc_tab_checks = [];

    if ( !isdefined( self.mc_tab_locks ) )
        self.mc_tab_locks = [];

    counter = self.mc_tab_counters[name];

    if ( need_counter && !isdefined( counter ) )
    {
        counter = newclienthudelem( self );
        counter.horzalign = "left";
        counter.vertalign = "top";
        counter.alignx = "center";
        counter.aligny = "middle";
        counter.fontscale = 1.17;
        counter.alpha = 1;
        counter.sort = 3;
        self.mc_tab_counters[name] = counter;
    }
    else if ( !need_counter && isdefined( counter ) )
    {
        counter destroy();
        self.mc_tab_counters[name] = undefined;
        counter = undefined;
    }

    if ( isdefined( counter ) )
    {
        counter.x = slot_x;
        counter.y = square_y + ( MC_TAB_SQUARE_SIZE / 2 ) + MC_TAB_BORDER_PAD + 6;
        counter settext( have + "/" + total );
    }

    check = self.mc_tab_checks[name];

    if ( need_check && !isdefined( check ) )
    {
        check = newclienthudelem( self );
        check.horzalign = "left";
        check.vertalign = "top";
        check.alignx = "center";
        check.aligny = "middle";
        check.alpha = 1;
        check.sort = 4;
        check setshader( "zm_hud_icon_sq_scafold", MC_TAB_CHECK_SIZE, MC_TAB_CHECK_SIZE );
        self.mc_tab_checks[name] = check;
    }
    else if ( !need_check && isdefined( check ) )
    {
        check destroy();
        self.mc_tab_checks[name] = undefined;
        check = undefined;
    }

    if ( isdefined( check ) )
    {
        check.x = slot_x + ( MC_TAB_SQUARE_SIZE / 2 ) - ( MC_TAB_CHECK_SIZE / 2 );
        check.y = square_y + ( MC_TAB_SQUARE_SIZE / 2 ) - ( MC_TAB_CHECK_SIZE / 2 );
    }

    lock = self.mc_tab_locks[name];

    if ( need_lock && !isdefined( lock ) )
    {
        lock = newclienthudelem( self );
        lock.horzalign = "left";
        lock.vertalign = "top";
        lock.alignx = "center";
        lock.aligny = "middle";
        lock.alpha = 1;
        lock.sort = 4;
        lock setshader( "zm_hud_icon_fan", MC_TAB_LOCK_SIZE, MC_TAB_LOCK_SIZE );
        self.mc_tab_locks[name] = lock;
    }
    else if ( !need_lock && isdefined( lock ) )
    {
        lock destroy();
        self.mc_tab_locks[name] = undefined;
        lock = undefined;
    }

    if ( isdefined( lock ) )
    {
        lock.x = slot_x + ( MC_TAB_SQUARE_SIZE / 2 ) - ( MC_TAB_LOCK_SIZE / 2 );
        lock.y = square_y + ( MC_TAB_SQUARE_SIZE / 2 ) - ( MC_TAB_LOCK_SIZE / 2 );
    }
}

mc_get_stub_origin( stub )
{
    if ( isdefined( stub.originfunc ) )
        return stub [[ stub.originfunc ]]();

    return stub.origin;
}

mc_count_remaining( zone )
{
    remaining = 0;

    for ( i = 0; i < zone.pieces.size; i++ )
    {
        if ( !( isdefined( zone.pieces[i].built ) && zone.pieces[i].built ) )
            remaining++;
    }

    return remaining;
}

mc_piece_key( piece )
{
    return piece.buildablename + "|" + piece.modelname;
}

mc_get_deliverable_pieces( zone )
{
    use_cache = !level.mc_is_buried;

    if ( use_cache )
    {
        now = gettime();

        if ( !isdefined( self.mc_deliverable_cache_tick ) || self.mc_deliverable_cache_tick != now )
        {
            self.mc_deliverable_cache = [];
            self.mc_deliverable_cache_tick = now;
        }

        if ( isdefined( self.mc_deliverable_cache[zone.buildable_name] ) )
            return self.mc_deliverable_cache[zone.buildable_name];
    }

    result = [];
    used = [];

    for ( i = 0; i < zone.pieces.size; i++ )
    {
        if ( isdefined( zone.pieces[i].built ) && zone.pieces[i].built )
            continue;

        key = mc_piece_key( zone.pieces[i] );

        pool = 0;
        if ( isdefined( level.mc_have[key] ) )
            pool = level.mc_have[key];

        consumed = 0;
        if ( isdefined( used[key] ) )
            consumed = used[key];

        if ( pool - consumed > 0 )
        {
            result[result.size] = zone.pieces[i];
            used[key] = consumed + 1;
        }
    }

    if ( use_cache )
        self.mc_deliverable_cache[zone.buildable_name] = result;

    return result;
}

player_collect_and_build()
{
    self endon( "disconnect" );
    if ( !isdefined( level.mc_buildables_ready ) )
        level waittill( "buildables_setup" );

    self thread mc_collect_loop();
    self thread mc_deliver_loop();
    self thread mc_key_use_loop();
}

mc_collect_loop()
{
    self endon( "disconnect" );

    while ( true )
    {
        self mc_try_collect();
        wait 0.05;
    }
}

mc_deliver_loop()
{
    self endon( "disconnect" );

    while ( true )
    {
        if ( level.mc_is_buried )
            self mc_try_deliver_buried();
        else
            self mc_try_deliver_default();

        wait 0.1;
    }
}

mc_key_use_loop()
{
    self endon( "disconnect" );

    while ( true )
    {
        self mc_try_use_key();
        wait 0.05;
    }
}

mc_try_use_key()
{
    if ( isdefined( self.mc_key_inserting ) && self.mc_key_inserting )
        return;

    if ( !isdefined( level.mc_key_stubs ) || level.mc_key_stubs.size == 0 )
        return;

    if ( !isdefined( self.mc_has_key ) || !self.mc_has_key )
        return;

    if ( self mc_key_cooldown_remaining() > 0 )
        return;

    if ( !self usebuttonpressed() )
        return;

    foreach ( stub in level.mc_key_stubs )
    {
        zone = stub.buildablezone;
        stub_origin = mc_get_stub_origin( stub );

        if ( !isdefined( stub_origin ) || !mc_in_range( self.origin, stub_origin, mc_build_radius_sq( zone.buildable_name ) ) )
            continue;

        if ( !mc_key_resolve_elevator( stub ) )
            continue;

        if ( isdefined( level.mc_elevator_is_on_floor_func ) && stub.elevator [[ level.mc_elevator_is_on_floor_func ]]( stub.floor ) )
            continue;

        self thread mc_key_insert_sequence( stub );
        return;
    }
}

mc_key_insert_sequence( stub )
{
    self endon( "disconnect" );
    self endon( "death" );

    if ( isdefined( self.mc_key_inserting ) && self.mc_key_inserting )
        return;

    self.mc_key_inserting = true;

    zone = stub.buildablezone;

    insert_bar = self createprimaryprogressbar();
    insert_bar_text = self createprimaryprogressbartext();
    insert_bar_text settext( "Insertando llave..." );

    start_time = gettime();
    success = true;

    while ( gettime() - start_time < MC_KEY_INSERT_TIME )
    {
        if ( !isdefined( self ) || !self usebuttonpressed() )
        {
            success = false;
            break;
        }

        stub_origin = mc_get_stub_origin( stub );

        if ( !isdefined( stub_origin ) || !mc_in_range( self.origin, stub_origin, mc_build_radius_sq( zone.buildable_name ) ) )
        {
            success = false;
            break;
        }

        if ( self mc_key_cooldown_remaining() > 0 )
        {
            success = false;
            break;
        }

        progress = ( gettime() - start_time ) / MC_KEY_INSERT_TIME;

        if ( progress < 0 )
            progress = 0;

        if ( progress > 1 )
            progress = 1;

        insert_bar updatebar( progress );

        wait 0.05;
    }

    insert_bar_text destroyelem();
    insert_bar destroyelem();

    self.mc_key_inserting = false;

    if ( !success || !isdefined( self ) )
        return;

    if ( isdefined( level.mc_elevator_is_on_floor_func ) && stub.elevator [[ level.mc_elevator_is_on_floor_func ]]( stub.floor ) )
        return;

    if ( isdefined( level.flag ) && isdefined( level.flag["power_on"] ) && level.flag["power_on"] )
    {
        if ( isdefined( stub.buildablestruct ) && isdefined( stub.buildablestruct.onuseplantobject ) )
            stub [[ stub.buildablestruct.onuseplantobject ]]( self );
    }

    self.mc_key_cooldown_end = gettime() + MC_KEY_COOLDOWN_MS;
    self playsound( "zmb_buildable_pickup" );
}

mc_try_collect()
{
    held_pieces = self player_get_buildable_pieces();

    if ( held_pieces.size == 0 )
        return;

    foreach ( held in held_pieces )
    {
        if ( !isdefined( held ) )
            continue;

        candidates = [];
        held_key = mc_piece_key( held );

        if ( level.mc_is_buried )
        {
            foreach ( stub in level.buildable_stubs )
            {
                if ( !isdefined( stub.buildablezone ) || !isdefined( stub.buildablezone.pieces ) )
                    continue;

                if ( !mc_is_ours( stub.buildablezone.buildable_name ) )
                    continue;

                if ( isdefined( stub.built ) && stub.built )
                    continue;

                if ( stub.buildablezone buildable_has_piece( held ) )
                    candidates[candidates.size] = stub;
            }
        }
        else
        {
            base_candidates = [];

            if ( isdefined( level.mc_piece_candidates ) && isdefined( level.mc_piece_candidates[held_key] ) )
                base_candidates = level.mc_piece_candidates[held_key];

            foreach ( stub in base_candidates )
            {
                if ( isdefined( stub.built ) && stub.built )
                    continue;

                candidates[candidates.size] = stub;
            }
        }

        if ( candidates.size == 0 )
            continue;

        key = held_key;

        if ( mc_is_key( candidates[0].buildablezone.buildable_name ) && isdefined( self.mc_has_key ) && self.mc_has_key )
            continue;

        count = 0;

        if ( isdefined( level.mc_have[key] ) )
            count = level.mc_have[key];

        level.mc_have[key] = count + 1;
        self.mc_last_pickup_time = gettime();
        nearest = candidates[0];
        nearest_dist = distancesquared( self.origin, mc_get_stub_origin( nearest ) );

        for ( i = 1; i < candidates.size; i++ )
        {
            d = distancesquared( self.origin, mc_get_stub_origin( candidates[i] ) );

            if ( d < nearest_dist )
            {
                nearest_dist = d;
                nearest = candidates[i];
            }
        }

        names = mc_display_name( candidates[0].buildablezone.buildable_name );

        for ( i = 1; i < candidates.size; i++ )
            names = names + " / " + mc_display_name( candidates[i].buildablezone.buildable_name );

        if ( level.mc_debug )
            println( "[mc_debug] piece " + held.buildablename + "/" + held.modelname + " -> candidates=" + candidates.size + " (available for everyone, no random values)" );

        progress_text = self mc_progress_text( nearest.buildablezone );

        if ( mc_is_key( nearest.buildablezone.buildable_name ) )
        {
            self.mc_has_key = true;

            self mc_show_key_pickup_notify( mc_representative_icon( nearest.buildablezone.buildable_name ) );
        }
        else
        {
            self mc_show_piece_notify( names, mc_representative_icon( nearest.buildablezone.buildable_name ), progress_text );
        }

        self player_destroy_piece( held );
    }
}

mc_try_deliver_default()
{
    if ( isdefined( self.mc_last_pickup_time ) && gettime() - self.mc_last_pickup_time < 400 )
        return;

    if ( !self usebuttonpressed() )
        return;

    foreach ( stub in level.buildable_stubs )
    {
        if ( !isdefined( stub.buildablezone ) || !isdefined( stub.buildablezone.pieces ) )
            continue;

        if ( !mc_is_ours( stub.buildablezone.buildable_name ) )
            continue;

        if ( mc_is_key( stub.buildablezone.buildable_name ) )
            continue;

        if ( isdefined( stub.built ) && stub.built )
            continue;

        zone = stub.buildablezone;
        stub_origin = mc_get_stub_origin( stub );

        if ( !mc_in_range( self.origin, stub_origin, mc_build_radius_sq( zone.buildable_name ) ) )
            continue;

        deliverable = self mc_get_deliverable_pieces( zone );
        can_attempt = false;

        if ( mc_is_gated( zone.buildable_name ) )
            can_attempt = deliverable.size > 0 && deliverable.size == mc_count_remaining( zone );
        else
            can_attempt = deliverable.size > 0;

        if ( !can_attempt )
            continue;

        if ( isdefined( stub.mc_original_prompt ) )
        {
            if ( !( stub [[ stub.mc_original_prompt ]]( self ) ) )
                continue;
        }

        success = self mc_do_build_hold( stub, zone );

        if ( success )
        {
            deliverable = self mc_get_deliverable_pieces( zone );
            self mc_deliver_pieces( zone, deliverable );
        }
    }
}

find_bench( bench_name )
{
    return getent( bench_name, "targetname" );
}

mc_swap_buildable_fields( stub1, stub2 )
{
    tbz = stub2.buildablezone;
    stub2.buildablezone = stub1.buildablezone;
    stub2.buildablezone.stub = stub2;
    stub1.buildablezone = tbz;
    stub1.buildablezone.stub = stub1;
    tbs = stub2.buildablestruct;
    stub2.buildablestruct = stub1.buildablestruct;
    stub1.buildablestruct = tbs;
    te = stub2.equipname;
    stub2.equipname = stub1.equipname;
    stub1.equipname = te;
    th = stub2.hint_string;
    stub2.hint_string = stub1.hint_string;
    stub1.hint_string = th;
    ths = stub2.trigger_hintstring;
    stub2.trigger_hintstring = stub1.trigger_hintstring;
    stub1.trigger_hintstring = ths;
    tp = stub2.persistent;
    stub2.persistent = stub1.persistent;
    stub1.persistent = tp;
    tobu = stub2.onbeginuse;
    stub2.onbeginuse = stub1.onbeginuse;
    stub1.onbeginuse = tobu;
    tocu = stub2.oncantuse;
    stub2.oncantuse = stub1.oncantuse;
    stub1.oncantuse = tocu;
    toeu = stub2.onenduse;
    stub2.onenduse = stub1.onenduse;
    stub1.onenduse = toeu;
    tt = stub2.target;
    stub2.target = stub1.target;
    stub1.target = tt;
    ttn = stub2.targetname;
    stub2.targetname = stub1.targetname;
    stub1.targetname = ttn;
    twn = stub2.weaponname;
    stub2.weaponname = stub1.weaponname;
    stub1.weaponname = twn;
    pav = stub2.original_prompt_and_visibility_func;
    stub2.original_prompt_and_visibility_func = stub1.original_prompt_and_visibility_func;
    stub1.original_prompt_and_visibility_func = pav;
    bench1 = undefined;
    bench2 = undefined;
    transfer_pos_as_is = 1;

    if ( isdefined( stub1.model ) && isdefined( stub2.model ) && isdefined( stub1.model.target ) && isdefined( stub2.model.target ) )
    {
        bench1 = find_bench( stub1.model.target );
        bench2 = find_bench( stub2.model.target );

        if ( isdefined( bench1 ) && isdefined( bench2 ) )
        {
            transfer_pos_as_is = 0;
            w2lo1 = bench1 worldtolocalcoords( stub1.model.origin );
            w2la1 = stub1.model.angles - bench1.angles;
            w2lo2 = bench2 worldtolocalcoords( stub2.model.origin );
            w2la2 = stub2.model.angles - bench2.angles;
            stub1.model.origin = bench2 localtoworldcoords( w2lo1 );
            stub1.model.angles = bench2.angles + w2la1;
            stub2.model.origin = bench1 localtoworldcoords( w2lo2 );
            stub2.model.angles = bench1.angles + w2la2;
        }

        tmt = stub2.model.target;
        stub2.model.target = stub1.model.target;
        stub1.model.target = tmt;
    }

    tm = stub2.model;
    stub2.model = stub1.model;
    stub1.model = tm;

    if ( transfer_pos_as_is && isdefined( stub1.model ) && isdefined( stub2.model ) )
    {
        tmo = stub2.model.origin;
        tma = stub2.model.angles;
        stub2.model.origin = stub1.model.origin;
        stub2.model.angles = stub1.model.angles;
        stub1.model.origin = tmo;
        stub1.model.angles = tma;
    }
}

mc_try_deliver_buried()
{
    if ( isdefined( self.mc_last_pickup_time ) && gettime() - self.mc_last_pickup_time < 400 )
        return;

    if ( !self usebuttonpressed() )
        return;

    near_bench_stub = undefined;
    best_dist = MC_BUILD_RADIUS_SQ;

    foreach ( stub in level.buildable_stubs )
    {
        if ( !isdefined( stub.buildablezone ) || !mc_is_ours( stub.buildablezone.buildable_name ) )
            continue;

        if ( isdefined( stub.table_built ) && stub.table_built )
            continue;

        if ( isdefined( stub.built ) && stub.built )
            continue;

        s_orig = mc_get_stub_origin( stub );
        if ( isdefined( s_orig ) && mc_in_range( self.origin, s_orig, MC_BUILD_RADIUS_SQ ) )
        {
            dist = distance2dsquared( self.origin, s_orig );
            if ( dist < best_dist )
            {
                best_dist = dist;
                near_bench_stub = stub;
            }
        }
    }

    if ( !isdefined( near_bench_stub ) )
        return;

    near_name = near_bench_stub.buildablezone.buildable_name;

    if ( mc_is_buried_fixed( near_name ) )
    {
        if ( mc_buried_tower_locked( near_name ) )
            return;

        zone = near_bench_stub.buildablezone;
        deliverable = self mc_get_deliverable_pieces( zone );
        can_attempt = deliverable.size > 0 && deliverable.size == mc_count_remaining( zone );

        if ( !can_attempt )
            return;

        if ( isdefined( near_bench_stub.mc_original_prompt ) )
        {
            if ( !( near_bench_stub [[ near_bench_stub.mc_original_prompt ]]( self ) ) )
                return;
        }

        near_bench_stub.bound_to_buildable = near_bench_stub;
        active_stub = near_bench_stub;

        success = self mc_do_build_hold( active_stub, active_stub.buildablezone );

        if ( success )
        {
            deliverable = self mc_get_deliverable_pieces( active_stub.buildablezone );
            self mc_deliver_pieces( active_stub.buildablezone, deliverable );

            active_stub.table_built = true;
            active_stub.built = true;
            active_stub.bound_to_buildable = undefined;
        }

        return;
    }

    target_stub = undefined;

    foreach ( stub in level.buildable_stubs )
    {
        if ( !isdefined( stub.buildablezone ) || !mc_is_ours( stub.buildablezone.buildable_name ) )
            continue;

        if ( mc_is_buried_fixed( stub.buildablezone.buildable_name ) )
            continue;

        if ( isdefined( stub.table_built ) && stub.table_built )
            continue;

        if ( isdefined( stub.built ) && stub.built )
            continue;

        zone = stub.buildablezone;
        deliverable = self mc_get_deliverable_pieces( zone );
        can_attempt = false;

        if ( mc_is_gated( zone.buildable_name ) )
            can_attempt = deliverable.size > 0 && deliverable.size == mc_count_remaining( zone );
        else
            can_attempt = deliverable.size > 0;

        if ( can_attempt )
        {
            target_stub = stub;
            break;
        }
    }

    if ( !isdefined( target_stub ) )
        return;

    if ( near_bench_stub != target_stub )
    {
        mc_swap_buildable_fields( near_bench_stub, target_stub );
    }

    near_bench_stub.bound_to_buildable = near_bench_stub;
    active_stub = near_bench_stub;

    target_b_name = active_stub.buildablezone.buildable_name;

    success = self mc_do_build_hold( active_stub, active_stub.buildablezone );

    if ( success )
    {
        deliverable = self mc_get_deliverable_pieces( active_stub.buildablezone );
        self mc_deliver_pieces( active_stub.buildablezone, deliverable );

        active_stub.table_built = true;
        active_stub.built = true;
        active_stub.bound_to_buildable = undefined;
    }
}

custom_pooledbuildable_stub_for_piece( piece )
{
    if ( !isdefined( piece ) )
        return undefined;

    if ( !isdefined( self.stubs ) )
        return undefined;

    foreach ( stub in level.buildable_stubs )
    {
        if ( isdefined( stub.buildablezone ) && stub.buildablezone buildable_has_piece( piece ) )
        {
            if ( isdefined( stub.bound_to_buildable ) && stub.bound_to_buildable == stub )
                return stub;
        }
    }

    foreach ( stub in level.buildable_stubs )
    {
        if ( isdefined( stub.buildablezone ) && stub.buildablezone buildable_has_piece( piece ) )
        {
            if ( !( isdefined( stub.built ) && stub.built ) )
                return stub;
        }
    }

    foreach ( stub in level.buildable_stubs )
    {
        if ( isdefined( stub.buildablezone ) && stub.buildablezone buildable_has_piece( piece ) )
            return stub;
    }

    return undefined;
}

mc_deliver_pieces( zone, pieces )
{
    for ( i = 0; i < pieces.size; i++ )
    {
        piece = pieces[i];

        if ( isdefined( zone.stub.buildablestruct ) && isdefined( zone.stub.buildablestruct.onuseplantobject ) )
        {
            self player_set_buildable_piece( piece, zone.buildable_slot );
            zone.stub [[ zone.stub.buildablestruct.onuseplantobject ]]( self );
        }

        one_piece = [];
        one_piece[0] = piece;
        self player_build( zone, one_piece );

        key = mc_piece_key( piece );

        if ( isdefined( level.mc_have[key] ) )
        {
            level.mc_have[key] = level.mc_have[key] - 1;

            if ( level.mc_have[key] <= 0 )
                level.mc_have[key] = undefined;
        }
    }
}

mc_do_build_hold( stub, zone )
{
    self endon( "disconnect" );
    self endon( "death" );

    build_time = MC_DEFAULT_BUILD_TIME;

    if ( isdefined( stub.usetime ) )
        build_time = stub.usetime;

    self disable_player_move_states( 1 );
    self increment_is_drinking();
    orgweapon = self getcurrentweapon();
    self giveweapon( "zombie_builder_zm" );
    self switchtoweapon( "zombie_builder_zm" );

    self.mc_buildaudio = spawn( "script_origin", self.origin );
    self.mc_buildaudio playloopsound( "zmb_buildable_loop" );

    self.mc_build_active = 1;
    start_time = gettime();
    self thread mc_build_progress_bar( start_time, build_time );
    self thread mc_build_dust_fx();

    success = true;

    while ( gettime() - start_time < build_time )
    {
        if ( !isdefined( self ) || !self usebuttonpressed() )
        {
            success = false;
            break;
        }

        stub_origin = mc_get_stub_origin( stub );

        if ( !mc_in_range( self.origin, stub_origin, mc_build_radius_sq( zone.buildable_name ) ) )
        {
            success = false;
            break;
        }

        wait 0.05;
    }

    self.mc_build_active = 0;

    if ( isdefined( self.mc_buildaudio ) )
    {
        self.mc_buildaudio delete();
        self.mc_buildaudio = undefined;
    }

    self maps\mp\zombies\_zm_weapons::switch_back_primary_weapon( orgweapon );
    self takeweapon( "zombie_builder_zm" );

    if ( isdefined( self.is_drinking ) && self.is_drinking )
        self decrement_is_drinking();

    self enable_player_move_states();

    return success;
}

mc_build_progress_bar( start_time, build_time )
{
    self endon( "disconnect" );
    self endon( "death" );

    usebar = self createprimaryprogressbar();
    usebartext = self createprimaryprogressbartext();
    usebartext settext( &"ZOMBIE_BUILDING" );

    while ( isdefined( self ) && isdefined( self.mc_build_active ) && self.mc_build_active && gettime() - start_time < build_time )
    {
        progress = ( gettime() - start_time ) / build_time;

        if ( progress < 0 )
            progress = 0;

        if ( progress > 1 )
            progress = 1;

        usebar updatebar( progress );
        wait 0.05;
    }

    usebartext destroyelem();
    usebar destroyelem();
}

mc_build_dust_fx()
{
    self endon( "disconnect" );
    self endon( "death" );

    while ( isdefined( self ) && isdefined( self.mc_build_active ) && self.mc_build_active )
    {
        playfx( level._effect["building_dust"], self getplayercamerapos(), self.angles );
        wait 0.5;
    }
}

mc_progress_text( zone )
{
    built = 0;

    for ( i = 0; i < zone.pieces.size; i++ )
    {
        if ( isdefined( zone.pieces[i].built ) && zone.pieces[i].built )
            built++;
    }

    deliverable = self mc_get_deliverable_pieces( zone );
    have = built + deliverable.size;

    return have + "/" + zone.pieces.size;
}